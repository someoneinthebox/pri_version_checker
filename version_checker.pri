defineReplace(checkVersion) {
    isEmpty(ARGS): error(No parameters taken. Check the function!)
    ARGS_SIZE = $$size(ARGS)
    SRC = $$first(ARGS)
    SRC_FN = $$basename(SRC)
    !exists($$SRC): error($$SRC is not exists. Check the path!)
    lessThan(ARGS_SIZE, 2): error(Not enough parameters on include $$SRC_FN)
    greaterThan(ARGS_SIZE, 2): warning(Extra parameters detected on include $$SRC_FN)
    VER = $$member(ARGS, 1)
    CAT_VAR = $$cat($$SRC, lines)
    CAT_VER_NAME = $$find(CAT_VAR, ._VERSION)
    isEmpty(CAT_VER_NAME): error($$SRC_FN: don\'t have version variable)
    CUR_INFO = $$split(CAT_VER_NAME, =)
    CUR_INFO_SIZE = $$size(CUR_INFO)
    lessThan(CUR_INFO_SIZE, 2): error($$SRC_FN: don\'t have version number)
    greaterThan(CUR_INFO_SIZE, 2): warning($$SRC_FN: $$first(CUR_INFO) has extra values)
    CUR_VER = $$member(CUR_INFO, 1)
    CUR_VER = $$replace(CUR_VER, \\s, "")
    !equals(CUR_VER, $$VER): error($$SRC_FN incompatibility. Current version: $${CUR_VER}. Needed version: $$VER)
    return($$SRC)
}
